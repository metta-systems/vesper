# Broadcom bcm2711 on Raspberry Pi 4 as JTAG target
# From https://gist.github.com/tnishinaga/46a3380e1f47f5e892bbb74e55b3cf3e
# See also https://xihan94.gitbook.io/raspberry-pi/raspberry-pi-4-bringup

if { [info exists CHIPNAME] } {
    set _CHIPNAME $CHIPNAME
} else {
    set _CHIPNAME bcm2711
}

if { [info exists DAP_TAPID] } {
    set _DAP_TAPID $DAP_TAPID
} else {
    set _DAP_TAPID 0x4ba00477
}

adapter speed 4000

transport select jtag
# we need to enable srst even though we don't connect it
reset_config trst_and_srst

jtag_ntrst_delay 500

telnet_port 4444
gdb_port 5555

#
# Main DAP
#
jtag newtap $_CHIPNAME tap -irlen 4 -expected-id $_DAP_TAPID
dap create $_CHIPNAME.dap -chain-position $_CHIPNAME.tap

set _TARGETNAME $_CHIPNAME.a72
set _CTINAME $_CHIPNAME.cti

set DBGBASE {0x80410000 0x80510000 0x80610000 0x80710000}
set CTIBASE {0x80420000 0x80520000 0x80620000 0x80720000}

set _cores 4
set _smp_command ""

for { set _core 0 } { $_core < $_cores } { incr _core } {
    cti create $_CTINAME.$_core -dap $_CHIPNAME.dap -ap-num 0 \
        -baseaddr [lindex $CTIBASE $_core]

    target create ${_TARGETNAME}.${_core} aarch64 \
        -dap ${_CHIPNAME}.dap -coreid $_core \
        -dbgbase [lindex $DBGBASE $_core] -cti ${_CTINAME}.${_core}

    if {$_core != 0} {
        set _smp_command "$_smp_command ${_TARGETNAME}.${_core}"
    } else {
        set _smp_command "target smp ${_TARGETNAME}.${_core}"
    }

    $_TARGETNAME.$_core configure -event reset-assert-post "aarch64 dbginit"
    $_TARGETNAME.$_core configure -event gdb-attach { halt }
}

eval $_smp_command
targets $_TARGETNAME.0
